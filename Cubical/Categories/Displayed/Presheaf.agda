{-# OPTIONS --safe #-}
module Cubical.Categories.Displayed.Presheaf where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Structure

open import Cubical.Data.Sigma

open import Cubical.Categories.Category
open import Cubical.Categories.Instances.Sets
open import Cubical.Categories.Presheaf
open import Cubical.Categories.Presheaf.Representable
open import Cubical.Categories.Functor
open import Cubical.Categories.Displayed.Base
open import Cubical.Categories.Displayed.Section.Base
open import Cubical.Categories.Displayed.Instances.Sets.Base
open import Cubical.Categories.Displayed.Functor

private
  variable
    ℓB ℓB' ℓC ℓC' ℓD ℓD' ℓP ℓP' : Level

open Category
open Functor
open Functorᴰ
open Section

-- equivalent to the data of a presheaf Pᴰ over ∫ D and a natural transformation
-- Pᴰ → P ∘ Fst
Presheafᴰ : {C : Category ℓC ℓC'} (D : Categoryᴰ C ℓD ℓD')
          → (P : Presheaf C ℓP) → (ℓP' : Level)
          → Type (ℓ-max (ℓ-max (ℓ-max (ℓ-max (ℓ-max ℓC ℓC') ℓD) ℓD') (ℓ-suc ℓP))
                    (ℓ-suc ℓP'))
Presheafᴰ {ℓP = ℓP} D P ℓP' = Functorᴰ P (D ^opᴰ) (SETᴰ ℓP ℓP')

module _ {C : Category ℓC ℓC'} (D : Categoryᴰ C ℓD ℓD')
         {P : Presheaf C ℓP} (Pᴰ : Presheafᴰ D P ℓP') where

  -- equivalent to the data of a universal element of Pᴰ such that the
  -- projection preserves the universality
  record UniversalElementᴰ (ue : UniversalElement C P)
    : Type (ℓ-max (ℓ-max (ℓ-max (ℓ-max ℓC ℓC') ℓD) ℓD') ℓP') where
    open UniversalElement ue
    open Categoryᴰ D
    field
      vertexᴰ : ob[ vertex ]
      elementᴰ : ⟨ Pᴰ .F-obᴰ vertexᴰ element ⟩
      universalᴰ : ∀ {x xᴰ}{f : C [ x , vertex ]}
                 → isEquiv λ (fᴰ : Hom[ f ][ xᴰ , vertexᴰ ]) →
                     Pᴰ .F-homᴰ fᴰ _ elementᴰ

-- Abstract nonsense formulation of this?
module _ {C : Category ℓC ℓC'} (D : Categoryᴰ C ℓD ℓD')
         {P : Presheaf C ℓP}
         (Pᴰ : Presheafᴰ D P ℓP')
         (F : GlobalSection D)
         where
  record PshSection : Type (ℓ-max (ℓ-max ℓC ℓC') (ℓ-max ℓP ℓP')) where
    field
      F-hom : ∀ {x} (p : ⟨ P ⟅ x ⟆ ⟩) → ⟨ Pᴰ .F-obᴰ (F .F-obᴰ x) p ⟩
      F-nat : ∀ {x y} (f : C [ x , y ])(p : ⟨ P ⟅ y ⟆ ⟩)
        → F-hom ((P ⟪ f ⟫) p) ≡ Pᴰ .F-homᴰ (F .F-homᴰ f) p (F-hom p)
