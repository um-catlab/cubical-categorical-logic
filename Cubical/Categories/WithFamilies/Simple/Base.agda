{-

  Simple categories with families are one approach to modeling the
  judgmental structure of simply typed lambda calculus. TODO: citation

-}
{-# OPTIONS --safe #-}
module Cubical.Categories.WithFamilies.Simple.Base where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Structure
open import Cubical.Foundations.Function
open import Cubical.Foundations.Equiv

open import Cubical.Data.Sigma as Sigma hiding (_Ã—_) 

open import Cubical.Categories.Category
open import Cubical.Categories.Functor
open import Cubical.Categories.NaturalTransformation
open import Cubical.Categories.Instances.Sets
open import Cubical.Categories.Limits.Terminal
open import Cubical.Categories.Limits.Terminal.More
open import Cubical.Categories.Presheaf
open import Cubical.Categories.Presheaf.More
open import Cubical.Categories.Presheaf.Constructions

open Category
open NatTrans

private
  variable
    â„“C â„“C' â„“T â„“T' â„“D â„“D' â„“S â„“S' : Level
SCwF : (â„“C â„“C' â„“T â„“T' : Level) â†’ Type _
SCwF â„“C â„“C' â„“T â„“T' =
  Î£[ C âˆˆ Category â„“C â„“C' ]
  Î£[ Ty âˆˆ Type â„“T ]
  Î£[ Tm âˆˆ (Ty â†’ Presheaf C â„“T') ]
  Terminal C Sigma.Ã—
  (âˆ€ (Î“ : C .ob) (A : Ty) â†’ UniversalElement C ((C [-, Î“ ]) Ã—ğ“Ÿ Tm A))

-- Sort of like a "Henkin morphism" or "lax morphism" because we don't
-- require it to preserve the universal properties (but all(?)
-- premorphisms do preserve the structure laxly so...)
PreMorphism : SCwF â„“C â„“C' â„“T â„“T' â†’ SCwF â„“D â„“D' â„“S â„“S' â†’ Type _
PreMorphism (C , CTy , CTm , Cterm , Ccomp) (D , DTy , DTm , Dterm , Dcomp) =
  Î£[ F âˆˆ Functor C D ]
  Î£[ F-Ty âˆˆ (CTy â†’ DTy) ]
  (âˆ€ (A : CTy) â†’ PshHomâ±½ (CTm A) (DTm (F-Ty A) âˆ˜F (F ^opF)))
  
-- Two options here:
-- 1. Strict: preserve the specified terminal/comprehension/projections up to Path
-- 2. Weak: image of the terminal object is terminal, image of the comprehension cone is universal
-- strict morphisms are always weak , the inverse follows if the SCwF is univalent
open UniversalElement
WkMorphism : SCwF â„“C â„“C' â„“T â„“T' â†’ SCwF â„“D â„“D' â„“S â„“S' â†’ Type _
WkMorphism (C , CTy , CTm , Cterm , Ccomp) (D , DTy , DTm , Dterm , Dcomp) =
  Î£[ F âˆˆ Functor C D ]
  Î£[ F-Ty âˆˆ (CTy â†’ DTy) ]
  Î£[ F-Tm âˆˆ (âˆ€ (A : CTy) â†’ PshHom F (CTm A) (DTm (F-Ty A))) ]
  isTerminal D (F âŸ… Cterm .fst âŸ†)
  Sigma.Ã— (âˆ€ (Î“ : C .ob) (A : CTy)
  â†’ isUniversal D (((D [-, (F âŸ… Î“ âŸ† ) ]) Ã—ğ“Ÿ DTm (F-Ty A)))
      (F âŸ… Ccomp Î“ A .vertex âŸ†)
      ((F âŸª Ccomp Î“ A .element .fst âŸ«) , F-Tm A .N-ob _ (lift $ Ccomp Î“ A .element .snd) .lower)) 

StrictMorphism : SCwF â„“C â„“C' â„“T â„“T' â†’ SCwF â„“D â„“D' â„“S â„“S' â†’ Type _
StrictMorphism (C , CTy , CTm , Cterm , Ccomp) (D , DTy , DTm , Dterm , Dcomp) =
  Î£[ F âˆˆ Functor C D ]
  Î£[ F-Ty âˆˆ (CTy â†’ DTy) ]
  Î£[ F-Tm âˆˆ (âˆ€ (A : CTy) â†’ PshHom F (CTm A) (DTm (F-Ty A))) ]
  (F âŸ… Cterm .fst âŸ† â‰¡ Dterm .fst)
  Sigma.Ã— (âˆ€ (Î“ : C .ob) (A : CTy) â†’
    Path (Î£[ FÎ“Ã—FA âˆˆ D .ob ] (D [ FÎ“Ã—FA , F âŸ… Î“ âŸ† ]) Sigma.Ã— âŸ¨ DTm (F-Ty A) âŸ… FÎ“Ã—FA âŸ† âŸ©)
      (F âŸ… Ccomp Î“ A .vertex âŸ† , F âŸª Ccomp Î“ A .element .fst âŸ« , F-Tm A .N-ob (Ccomp Î“ A .vertex) (lift (Ccomp Î“ A .element .snd)) .lower)
      (Dcomp (F âŸ… Î“ âŸ†) (F-Ty A) .vertex , Dcomp (F âŸ… Î“ âŸ†) (F-Ty A) .element .fst , Dcomp (F âŸ… Î“ âŸ†) (F-Ty A) .element .snd)
      )
module SCwFNotation (C : SCwF â„“C â„“C' â„“T â„“T') where
  ğ“’ = C .fst
  Ty = C .snd .fst
  Tm = C .snd .snd .fst
  open TerminalNotation ğ“’ (C .snd .snd .snd .fst) public

  _Ã—_ : ğ“’ .ob â†’ Ty â†’ ğ“’ .ob
  Î“ Ã— A = C .snd .snd .snd .snd Î“ A .vertex

  Ï€â‚ : âˆ€ {Î“ A} â†’ ğ“’ [ Î“ Ã— A , Î“ ]
  Ï€â‚ = C .snd .snd .snd .snd _ _ .element .fst
  
  Ï€â‚‚ : âˆ€ {Î“ A} â†’ âŸ¨ Tm A âŸ… Î“ Ã— A âŸ† âŸ©
  Ï€â‚‚ = C .snd .snd .snd .snd _ _ .element .snd

  open isEquiv
  _,p_ : âˆ€ {Î” Î“ A} â†’ ğ“’ [ Î” , Î“ ] â†’ âŸ¨ Tm A âŸ… Î” âŸ† âŸ© â†’ ğ“’ [ Î” , Î“ Ã— A ]
  Î³ ,p M = C .snd .snd .snd .snd _ _ .universal _ .equiv-proof (Î³ , M) .fst .fst
            
