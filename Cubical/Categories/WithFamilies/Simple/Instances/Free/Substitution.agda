{-# OPTIONS -WnoUnsupportedIndexedMatch #-}
{-# OPTIONS --lossy-unification #-}

-- Free Simple Category with families generated by a signature
module Cubical.Categories.WithFamilies.Simple.Instances.Free.Substitution where

open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Function
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Structure
open import Cubical.Foundations.More

open import Cubical.Data.FinData hiding (elim)
open import Cubical.Data.List hiding (elim; [_])
open import Cubical.Data.List.FinData
open import Cubical.Data.List.Dependent
open import Cubical.Data.Sigma
open import Cubical.Data.Unit
open import Cubical.Data.W.Indexed

open import Cubical.Categories.Category.Base
open import Cubical.Categories.Constructions.Fiber hiding (fiber)
open import Cubical.Categories.Constructions.TotalCategory using (∫C)
open import Cubical.Categories.Functor
open import Cubical.Categories.Limits.Terminal.More
open import Cubical.Categories.Presheaf
open import Cubical.Categories.Presheaf.Constructions
open import Cubical.Categories.Presheaf.More

open import Cubical.Categories.Displayed.Base
open import Cubical.Categories.Displayed.Functor
open import Cubical.Categories.Displayed.Section
open import Cubical.Categories.Displayed.Presheaf

open import Cubical.Categories.WithFamilies.Simple.Base
open import Cubical.Categories.WithFamilies.Simple.Instances.Free.Variable

private
  variable
    ℓ ℓ' ℓC ℓC' ℓT ℓT' : Level

open Category
open Functor
open Functorᴰ
open Section
open UniversalElement

module _ (Σ₀ : hGroupoid ℓ) where
  private
    variable
      Δ Γ Θ Ξ : List ⟨ Σ₀ ⟩
      A B C : ⟨ Σ₀ ⟩

  record Signature ℓ' : Type (ℓ-max ℓ (ℓ-suc ℓ')) where
    field
      Op : Type ℓ'
      dom : Op → List ⟨ Σ₀ ⟩
      cod : Op → ⟨ Σ₀ ⟩
      isSetOp : isSet Op

  module _ (Σ₁ : Signature ℓ') where
    private
      module Σ₁ = Signature Σ₁

    data Term (Γ : List ⟨ Σ₀ ⟩) : ⟨ Σ₀ ⟩ → Type (ℓ-max ℓ ℓ')
    Subst : (Δ : List ⟨ Σ₀ ⟩)(Γ : List ⟨ Σ₀ ⟩) → Type (ℓ-max ℓ ℓ')
    Subst Δ Γ = ListP (Term Δ) Γ
    data Term Γ where
      var : ∀ {A} → Var _ Γ A → Term Γ A
      op : (o : Σ₁.Op) (δ : Subst Γ (Σ₁.dom o)) → Term Γ (Σ₁.cod o)

    substTerm : ∀ {Δ Γ A} → Subst Δ Γ → Term Γ A → Term Δ A
    _⋆Sub_ : Subst Θ Δ → Subst Δ Γ → Subst Θ Γ
    substTerm (M ∷ γ) (var vz) = M
    substTerm (M ∷ γ) (var (vs x)) = substTerm γ (var x)
    substTerm γ (op o δ) = op o (γ ⋆Sub δ)
    δ ⋆Sub [] = []
    δ ⋆Sub (M ∷ θ) = substTerm δ M ∷ (δ ⋆Sub θ)

    isSetTerm : ∀ Γ A → isSet (Term Γ A)
    isSetTerm Γ = λ A → isSetRetract (toW A) (fromW A) (retr A) (isSetTerm' A)
      where
        open import Cubical.Data.Sum as Sum
        open import Cubical.Data.Empty as Empty

        Term' : ⟨ Σ₀ ⟩ → Type _
        Term' = IW (λ A → FinVar _ Γ A ⊎ fiber Σ₁.cod A)
                   (λ A → Sum.rec (λ _ → Empty.⊥*) λ (o , pf) → Fin (length (Σ₁.dom o))) -- anyvar type?
                   (λ A → Sum.elim (λ _ → Empty.rec*) λ (o , pf) → lookup (Σ₁.dom o))

        Subst' : List ⟨ Σ₀ ⟩ → Type _
        Subst' Δ = (x : AnyVar _ Δ) → Term' (lookup Δ x)

        substToW : ∀ Δ → Subst Γ Δ → Subst' Δ
        toW : ∀ A → Term Γ A → Term' A
        substToW Δ [] = λ ()
        substToW Δ (M ∷ δ) zero = toW _ M
        substToW Δ (M ∷ δ) (suc x) = substToW _ δ x

        toW A (var x) = node (inl (Var→FinVar _ x)) Empty.elim*
        toW A (op o δ) = node (inr (o , refl)) (substToW (Σ₁.dom o) δ)

        substFromW' : ∀ Δ → Subst' Δ → ListP Term' Δ
        substFromW' = tabulateOverLookup
        
        substFromW : ∀ Δ → Subst' Δ → Subst Γ Δ
        fromW : ∀ A → Term' A → Term Γ A

        fromW A (node (inl x) subtree) = var (FinVar→Var _ x)
        fromW A (node (inr (o , o:A)) subtree) =
          subst (Term Γ) o:A (op o (substFromW _ subtree))
        substFromW Δ δ = tabulateOverLookup Δ (λ x → fromW _ (δ x))

        retrSubst : ∀ Δ δ → substFromW Δ (substToW Δ δ) ≡ δ
        retr : ∀ A M → fromW A (toW A M) ≡ M
        retrSubst [] [] i = []
        retrSubst (A ∷ Δ) (M ∷ δ) i = retr A M i ∷ retrSubst Δ δ i

        retr A (var x) = cong var (VarRetractFinVar ⟨ Σ₀ ⟩ x)
        retr A (op o δ) =
          subst (Term Γ) (λ _ → Σ₁.cod o) (op o ((tabulateOverLookup (Σ₁.dom o) (λ x → fromW (lookup (Σ₁.dom o) x) (substToW (Σ₁.dom o) δ x))))) ≡⟨ substRefl {B = Term Γ} _ ⟩
          (op o ((tabulateOverLookup (Σ₁.dom o) (λ x → fromW (lookup (Σ₁.dom o) x) (substToW (Σ₁.dom o) δ x)))))
            ≡⟨ cong (op o) (retrSubst _ δ) ⟩
          op o δ ∎

        isSetTerm' : ∀ A → isSet (Term' A)
        isSetTerm' = isOfHLevelSuc-IW 1 (λ A → isSet⊎ (isSetΣ isSetFin (λ x → Σ₀ .snd (lookup Γ x) A)) (isSetΣ Σ₁.isSetOp (λ o → Σ₀ .snd _ _)))
